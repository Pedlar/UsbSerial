apply plugin: 'com.android.library'
apply plugin: 'android-maven'

group = 'com.felhr.usbserial'
version = '4.5-BYPASS'

android {

    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }
}

def isReleaseBuild() {
    return !version.contains("SNAPSHOT")
}

def getBypassRepoWriteUser(boolean isReleaseBuild) {
    if (isReleaseBuild) {
        return hasProperty('bypassReleaseWriteUser') ? bypassReleaseWriteUser : ""
    }
    return hasProperty('bypassSnapshotWriteUser') ? bypassSnapshotWriteUser : ""
}

def getBypassRepoWritePassword(boolean isReleaseBuild) {
    if (isReleaseBuild) {
        return hasProperty('bypassReleaseWritePassword') ? bypassReleaseWritePassword : ""
    }
    return hasProperty('bypassSnapshotWritePassword') ? bypassSnapshotWritePassword : ""
}

def getRepoUrl(boolean isReleaseBuild) {
    if (isReleaseBuild) {
        return hasProperty('bypassReleaseRepoUrl') ? bypassReleaseRepoUrl : "";
    }
    return hasProperty('bypassSnapshotRepoUrl') ? bypassSnapshotRepoUrl : "";
}

uploadArchives {
    repositories.mavenDeployer {
        repository(url: getRepoUrl(isReleaseBuild())) {
            authentication(userName: getBypassRepoWriteUser(isReleaseBuild()), password: getBypassRepoWritePassword(isReleaseBuild()))
        }
        pom.artifactId = 'usbserial'
    }
}

task release(dependsOn: uploadArchives)
